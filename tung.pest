COMMENT = _{ "//" ~ (!"\n" ~ ANY)* ~ ("\n" | EOI) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
WHITESPACE = _{ " " | "\t" | "\n" | "\r" | COMMENT }

block = { "{" ~ statement* ~ "}" }

program = { SOI ~ statement* ~ EOI }

statement = _{
    variable_declaration
    | assignment
    | print_statement
    | if_statement
    | while_statement
}

variable_declaration = { "var" ~ IDENTIFIER ~ "=" ~ expression }
assignment = { IDENTIFIER ~ "=" ~ expression }
print_statement = { "print" ~ "(" ~ expression ~ ")" }
if_statement = { "if" ~ expression ~ block ~ elif_block* ~ else_block? }
elif_block = { "elif" ~ expression ~ block }
else_block = { "else" ~ block }

while_statement = { "while" ~ expression ~ block }

expression = _{ comparison }
comparison = { sum ~ (comp_op ~ sum)* }
comp_op = { "==" | "!=" | "<=" | ">=" | "<" | ">" }
sum = { term ~ (add_op ~ term)* }
add_op = { "+" | "-" }
term = { factor ~ (mul_op ~ factor)* }
mul_op = { "*" | "/" }
factor = { function_call | number | string | IDENTIFIER | "(" ~ expression ~ ")" }
function_call = { IDENTIFIER ~ "(" ~ (expression ~ ("," ~ expression)*)? ~ ")" }

 string = @{ "\"" ~ (escape_sequence | (!"\"" ~ ANY))* ~ "\"" }
 escape_sequence = _{ "\\" ~ ("\"" | "n" | "t" | "r" | "\\" | "0") }
number = @{ ASCII_DIGIT+ }
 IDENTIFIER = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }