WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

program = { SOI ~ statement* ~ EOI }

statement = _{
    variable_declaration
    | print_statement
    | if_statement
}

variable_declaration = { "var" ~ IDENTIFIER ~ "=" ~ expression }
print_statement = { "print" ~ "(" ~ expression ~ ")" }
if_statement = { "if" ~ expression ~ "{" ~ statement* ~ "}" ~ elif_block* ~ else_block? }
elif_block = { "elif" ~ expression ~ "{" ~ statement* ~ "}" }
else_block = { "else" ~ "{" ~ statement* ~ "}" }

expression = _{ comparison }
comparison = { sum ~ (comp_op ~ sum)* }
comp_op = { "==" | "!=" | "<=" | ">=" | "<" | ">" }
sum = { term ~ (add_op ~ term)* }
add_op = { "+" | "-" }
term = { factor ~ (mul_op ~ factor)* }
mul_op = { "*" | "/" }
factor = { function_call | number | string | IDENTIFIER | "(" ~ expression ~ ")" }
function_call = { IDENTIFIER ~ "(" ~ (expression ~ ("," ~ expression)*)? ~ ")" }

string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
number = @{ ASCII_DIGIT+ }
IDENTIFIER = @{ ASCII_ALPHANUMERIC+ }